plugins {
  id 'com.github.johnrengelman.shadow' version '6.1.0'
}

apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = 11
targetCompatibility = 11
sourceSets.main.java.srcDirs = ['src']
sourceSets.main.java.outputDir = file('bin')
sourceSets.test.java.srcDirs = ['test/src']
// buildDir = 'build'

wrapper {
  gradleVersion = '6.6'
}

task delombok(type: Exec, dependsOn: compileJava) {
  doFirst {
    def sourceDirName = "build/delombok/src"
    def srcDir = file(sourceDirName)
    srcDir.deleteDir()
    srcDir.mkdir()
  }
  commandLine "powershell", "-Command", "java", "-jar", "\"lib\\lombok-1.18.16.jar\"", "delombok", "src", "--classpath", "\"build\\libs\\jisd-all.jar\"", "-d", "build/delombok/src"
}

javadoc {
  dependsOn delombok
  def sourceDirName = "build/delombok/src"
  source = sourceDirName
  exclude('jisd/demo/**')
  exclude('jisd/util/**')
  destinationDir = file("../JISD-doc/docs")
  failOnError = true
  options.setMemberLevel(JavadocMemberLevel.PUBLIC)
  options.addStringOption('Xdoclint:none', '-quiet')
  doLast {
    copy {
      from "../JISD-doc/.nojekyll"
      into "../JISD-doc/docs"
    }
  }
}

jar {
  archiveBaseName = 'jisd'
  archiveVersion = '0.0.2'
  exclude('.jisd_static_data*')
}

shadowJar {
  dependsOn jar
  exclude('jisd/demo/**')
  exclude('.jisd_static_data*')
}

test {
  useJUnitPlatform()
}

repositories {
  mavenCentral()
}

dependencies {
  // https://mvnrepository.com/artifact/org.ow2.asm/asm
  implementation 'org.ow2.asm:asm:9.0'

  // https://mvnrepository.com/artifact/org.ow2.asm/asm-tree
  implementation 'org.ow2.asm:asm-tree:9.0'

  // https://mvnrepository.com/artifact/org.json/json
  implementation 'org.json:json:20171018'

  // https://mvnrepository.com/artifact/commons-io/commons-io
  implementation 'commons-io:commons-io:2.8.0'


  // jdiscript
  implementation files('../jdiscript/jdiscript/build/libs/jdiscript-0.9.0.jar')

  // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.2'

  // https://mvnrepository.com/artifact/org.projectlombok/lombok
  compileOnly 'org.projectlombok:lombok:1.18.16'
  annotationProcessor 'org.projectlombok:lombok:1.18.16'

}

task createKernelJson(type: JavaExec) {
  main = "util.JISDPreProcess"
  classpath = sourceSets.main.runtimeClasspath
  def path = project.hasProperty("jsonpath") ? project.property("jsonpath") : ""
  def cp = project.hasProperty("cp") ? project.property("cp") : ""
  args(path, path, cp)
}

tasks.withType(JavaCompile) {
  options.compilerArgs << '-Xlint:unchecked'
}
